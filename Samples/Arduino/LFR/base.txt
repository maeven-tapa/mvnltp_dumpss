/*
 Sample Line Following Code for ESP32-S3-N16R8 with TB6612FNG
*/

#include <SparkFun_TB6612.h>

// TB6612FNG Motor Driver Pins
#define AIN1 36
#define AIN2 35
#define PWMA 21
#define BIN1 38
#define BIN2 39
#define PWMB 40
#define STBY 37

// TCRT5000 5-Channel Analog Sensor Pins
#define SENSOR_LEFT 15    // OUT1
#define SENSOR_L2 7       // OUT2
#define SENSOR_CENTER 6   // OUT3
#define SENSOR_R2 5       // OUT4
#define SENSOR_RIGHT 4    // OUT5

// Pull-down Buttons
#define BUTTONA 20
#define BUTTONB 1

// these constants are used to allow you to make your motor configuration
// line up with function names like forward.  Value can be 1 or -1
const int offsetA = 1;
const int offsetB = 1;

// Initializing motors.  The library will allow you to initialize as many
// motors as you have memory for.  If you are using functions like forward
// that take 2 motors as arguements you can either write new functions or
// call the function more than once.

Motor motor1 = Motor(AIN1, AIN2, PWMA, offsetA, STBY);
Motor motor2 = Motor(BIN1, BIN2, PWMB, offsetB, STBY);


int P, D, I, previousError, PIDvalue, error;
int lsp, rsp;
int lfspeed = 200;

float Kp = 0;
float Kd = 0;
float Ki = 0 ;


int minValues[6], maxValues[6], threshold[6];

void setup()
{
  Serial.begin(9600);
  pinMode(BUTTONA, INPUT);  // Pull-down buttons
  pinMode(BUTTONB, INPUT);  // Pull-down buttons
}


void loop()
{
  while (!digitalRead(BUTTONA)) {}  // Wait for button press (pull-down)
  delay(1000);
  calibrate();
  while (!digitalRead(BUTTONB)) {}  // Wait for button press (pull-down)
  delay(1000);

  while (1)
  {
    if (analogRead(SENSOR_L2) > threshold[1] && analogRead(SENSOR_RIGHT) < threshold[5] )
    {
      lsp = 0; rsp = lfspeed;
      motor1.drive(0);
      motor2.drive(lfspeed);
    }

    else if (analogRead(SENSOR_RIGHT) > threshold[5] && analogRead(SENSOR_L2) < threshold[1])
    { lsp = lfspeed; rsp = 0;
      motor1.drive(lfspeed);
      motor2.drive(0);
    }
    else if (analogRead(SENSOR_CENTER) > threshold[3])
    {
      Kp = 0.0006 * (1000 - analogRead(SENSOR_CENTER));
      Kd = 10 * Kp;
      //Ki = 0.0001;
      linefollow();
    }
  }
}

void linefollow()
{
  int error = (analogRead(SENSOR_L2) - analogRead(SENSOR_R2));

  P = error;
  I = I + error;
  D = error - previousError;

  PIDvalue = (Kp * P) + (Ki * I) + (Kd * D);
  previousError = error;

  lsp = lfspeed - PIDvalue;
  rsp = lfspeed + PIDvalue;

  if (lsp > 255) {
    lsp = 255;
  }
  if (lsp < 0) {
    lsp = 0;
  }
  if (rsp > 255) {
    rsp = 255;
  }
  if (rsp < 0) {
    rsp = 0;
  }
  motor1.drive(lsp);
  motor2.drive(rsp);

}

void calibrate()
{
  int sensorPins[5] = {SENSOR_LEFT, SENSOR_L2, SENSOR_CENTER, SENSOR_R2, SENSOR_RIGHT};
  
  for ( int i = 1; i < 6; i++)
  {
    minValues[i] = analogRead(sensorPins[i-1]);
    maxValues[i] = analogRead(sensorPins[i-1]);
  }
  
  for (int i = 0; i < 3000; i++)
  {
    motor1.drive(50);
    motor2.drive(-50);

    for ( int i = 1; i < 6; i++)
    {
      if (analogRead(sensorPins[i-1]) < minValues[i])
      {
        minValues[i] = analogRead(sensorPins[i-1]);
      }
      if (analogRead(sensorPins[i-1]) > maxValues[i])
      {
        maxValues[i] = analogRead(sensorPins[i-1]);
      }
    }
  }

  for ( int i = 1; i < 6; i++)
  {
    threshold[i] = (minValues[i] + maxValues[i]) / 2;
    Serial.print(threshold[i]);
    Serial.print("   ");
  }
  Serial.println();
  
  motor1.drive(0);
  motor2.drive(0);
}